
# Lab 03: ESP32 HTTP Client - Website Communication

**Learn to send and receive data from external websites and APIs**

Transform your ESP32 into an HTTP client that communicates with external web services, enabling integration with weather APIs, IoT platforms, and cloud databases.

----------

## Objectives

-   Configure ESP32 as an HTTP client to send GET and POST requests to external websites.
    
-   Parse HTTP responses including status codes, headers, and body content.
    
-   Communicate with REST APIs to retrieve JSON data and send sensor readings.
    
-   Understand the difference between ESP32 as server (Lab 02) vs. client (this lab).
    

----------

## What You'll Learn

-   HTTPClient library usage for making web requests from ESP32.
    
-   HTTP request methods: GET for retrieving data, POST for sending data to servers.
    
-   JSON parsing using ArduinoJson library to extract specific data from API responses.
    
-   Real-world API integration with public services (weather data, time servers, IoT platforms).
    

----------

## Prerequisites

-   **Completed Lab 01 and 02:**  WiFi connection and web server basics.
    
-   HTTPClient library (built-in with ESP32 Arduino core).
    
-   ArduinoJson library (install via Library Manager: Sketch → Include Library → Manage Libraries → search "ArduinoJson").
    
-   Understanding of URLs and query parameters.
    


## HTTP Client vs Server

### ESP32 as Server (Lab 02)

-   ESP32 waits for incoming connections from browsers/apps
    
-   ESP32 responds to requests with HTML pages or data
    
-   Users connect to ESP32's IP address
    
-   Example: Web dashboard to control LEDs
    

### ESP32 as Client (This Lab)

-   ESP32 initiates connections to external websites/APIs
    
-   ESP32 sends requests and receives responses
    
-   ESP32 connects to public or private servers by URL
    
-   Example: Fetching weather data from OpenWeatherMap API
    

Both roles can coexist in the same project for full bidirectional IoT communication.

## HTTP Request Methods: GET vs POST

The Hypertext Transfer Protocol (HTTP) works as a request-response protocol between a client and server. Here’s an example:

-   The ESP32 (client) submits an HTTP request to a Raspberry Pi running Node-RED (server);
-   The server returns a response to the ESP32 (client);
-   Finally, the response contains status information about the request and may also contain the requested content.

### HTTP GET

**GET** is used to request data from a specified resource. It is often used to get values from APIs.

For example, you can have:

```
GET /update-sensor?temperature=value1
```

Note that the query string (_name = temperature_  and  _value = value1_) is sent in the URL of the HTTP GET request.

Or you can use a simple request to return a value or JSON object, for example:

```
GET /get-sensor
```

_(With HTTP GET, data is visible to everyone in the URL request.)_

### HTTP POST

**POST** is used to send data to a server to create/update a resource. For example, publish sensor readings to a server.

The data sent to the server with POST is stored in the request body of the HTTP request:

```
POST /update-sensor HTTP/1.1
Host: example.com
api_key=api&sensor_name=name&temperature=value1&humidity=value2&pressure=value3
Content-Type: application/x-www-form-urlencoded
```

In the body request, you can also send a JSON object:

```
POST /update-sensor HTTP/1.1
Host: example.com
{api_key: "api", sensor_name: "name", temperature: value1, humidity: value2, pressure: value3}
Content-Type: application/json
```

_(With HTTP POST, data is not visible in the URL request. However, if it’s not encrypted, it’s still visible in the request body.)_

### Arduino_JSON Library

You also need to install the [Arduino_JSON library](https://github.com/arduino-libraries/Arduino_JSON). You can install this library in the Arduino IDE Library Manager. Just go to **Sketch** > **Include Library** > **Manage Libraries** and search for the library name as follows:

![Install Arduino JSON library Arduino IDE](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2020/01/Install-Arduino-JSON-library-Arduino-IDE.png?resize=786%2C443&quality=100&strip=all&ssl=1)

## 1. ESP32 HTTP GET: Value or Query in URL

In the first example, the ESP32 will make an HTTP GET request to update a reading in a service. This type of request could also be used to filter a value, request a value, or return a JSON object.

![HTTP GET ESP32 Get Sensor Value Plain Text Status 200 OK](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2020/04/HTTP-GET-ESP32-Get-Sensor-Value-Plain-Text-OK.png?resize=828%2C392&quality=100&strip=all&ssl=1)

### Code ESP32 HTTP GET with Arduino IDE

After installing the necessary board add-ons and libraries, copy the following code to your Arduino IDE, but don’t upload it yet. You need to make some changes to make it work for you.

```c

#include <WiFi.h>
#include <HTTPClient.h>

const char* ssid = "REPLACE_WITH_YOUR_SSID";
const char* password = "REPLACE_WITH_YOUR_PASSWORD";

//Your Domain name with URL path or IP address with path
String serverName = "http://192.168.1.106:1880/update-sensor";

// the following variables are unsigned longs because the time, measured in
// milliseconds, will quickly become a bigger number than can be stored in an int.
unsigned long lastTime = 0;
// Timer set to 10 minutes (600000)
//unsigned long timerDelay = 600000;
// Set timer to 5 seconds (5000)
unsigned long timerDelay = 5000;

void setup() {
  Serial.begin(115200); 

  WiFi.begin(ssid, password);
  Serial.println("Connecting");
  while(WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.print("Connected to WiFi network with IP Address: ");
  Serial.println(WiFi.localIP());
 
  Serial.println("Timer set to 5 seconds (timerDelay variable), it will take 5 seconds before publishing the first reading.");
}

void loop() {
  //Send an HTTP POST request every 10 minutes
  if ((millis() - lastTime) > timerDelay) {
    //Check WiFi connection status
    if(WiFi.status()== WL_CONNECTED){
      HTTPClient http;

      String serverPath = serverName + "?temperature=24.37";
      
      // Your Domain name with URL path or IP address with path
      http.begin(serverPath.c_str());
      
      // If you need Node-RED/server authentication, insert user and password below
      //http.setAuthorization("REPLACE_WITH_SERVER_USERNAME", "REPLACE_WITH_SERVER_PASSWORD");
      
      // Send HTTP GET request
      int httpResponseCode = http.GET();
      
      if (httpResponseCode>0) {
        Serial.print("HTTP Response code: ");
        Serial.println(httpResponseCode);
        String payload = http.getString();
        Serial.println(payload);
      }
      else {
        Serial.print("Error code: ");
        Serial.println(httpResponseCode);
      }
      // Free resources
      http.end();
    }
    else {
      Serial.println("WiFi Disconnected");
    }
    lastTime = millis();
  }
}

```

[View raw code](https://github.com/RuiSantosdotme/Random-Nerd-Tutorials/raw/master/Projects/ESP32/HTTP/ESP32_HTTP_GET_Update.ino)

#### Setting your network credentials

Modify the next lines with your network credentials: SSID and password. The code is well commented on where you should make the changes.

```c
// Replace with your network credentials
const char* ssid     = "REPLACE_WITH_YOUR_SSID";
const char* password = "REPLACE_WITH_YOUR_PASSWORD";
```

#### Setting your serverName

You also need to type your domain name or Node-RED IP address, so the ESP publishes the readings to your own server.

```c
String serverName = "http://192.168.1.106:1880/update-sensor";
```

Now, upload the code to your board and it should work straight away.

Read the next section, if you want to learn how to make the HTTP GET request.

#### HTTP GET Request

In the  loop()  is where you actually make the HTTP GET request every 5 seconds with sample data:

```c
String serverPath = serverName + "?temperature=24.37";

// Your Domain name with URL path or IP address with path
http.begin(serverPath.c_str());

// If your need Node-RED/server authentication, insert user and password below
//http.setAuthorization("REPLACE_WITH_SERVER_USERNAME", "REPLACE_WITH_SERVER_PASSWORD");

// Send HTTP GET request
int httpResponseCode = http.GET();
```

Note: if Node-RED requires authentication, uncomment the following line and insert the Node-RED username and password.

```c
// If you need Node-RED/server authentication, insert user and password below
//http.setAuthorization("REPLACE_WITH_SERVER_USERNAME", "REPLACE_WITH_SERVER_PASSWORD");
```

The ESP32 makes a new request in the following URL to update the sensor field with a new temperature.

```
http://192.168.1.106:1880/update-sensor?temperature=24.37
```

Then, the following lines of code save the HTTP response from the server.

```c
if (httpResponseCode>0) {
  Serial.print("HTTP Response code: ");
  Serial.println(httpResponseCode);
  String payload = http.getString();
  Serial.println(payload);
}
else {
  Serial.print("Error code: ");
  Serial.println(httpResponseCode);
}
```

### Demonstration

With your board running the new sketch, open the Node-RED debug window. You’ll see that the sample values are being printed successfully (24.37).

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2020/04/udpate-data-esp32-HTTP-GET-ESP8266-NodeMCU-Arduino-value.png?resize=302%2C220&quality=100&strip=all&ssl=1)

## 2. ESP32 HTTP GET: JSON Data Object or Plain Text

This next example shows how to make an HTTP GET request to get a JSON object and decode it with the ESP32. Many APIs return data in JSON format.

![HTTP GET ESP32 Get Sensor JSON Data](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2020/04/HTTP-GET-ESP32-Get-Sensor-JSON-Data.png?resize=828%2C392&quality=100&strip=all&ssl=1)

Copy the next sketch to your Arduino IDE (type your SSID and password):

```c

#include <WiFi.h>
#include <HTTPClient.h>
#include <Arduino_JSON.h>

const char* ssid = "REPLACE_WITH_YOUR_SSID";
const char* password = "REPLACE_WITH_YOUR_PASSWORD";

//Your Domain name with URL path or IP address with path
const char* serverName = "http://192.168.1.106:1880/get-sensor";

// the following variables are unsigned longs because the time, measured in
// milliseconds, will quickly become a bigger number than can be stored in an int.
unsigned long lastTime = 0;
// Timer set to 10 minutes (600000)
//unsigned long timerDelay = 600000;
// Set timer to 5 seconds (5000)
unsigned long timerDelay = 5000;

String sensorReadings;
float sensorReadingsArr[3];

void setup() {
  Serial.begin(115200);

  WiFi.begin(ssid, password);
  Serial.println("Connecting");
  while(WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.print("Connected to WiFi network with IP Address: ");
  Serial.println(WiFi.localIP());
 
  Serial.println("Timer set to 5 seconds (timerDelay variable), it will take 5 seconds before publishing the first reading.");
}

void loop() {
  //Send an HTTP POST request every 10 minutes
  if ((millis() - lastTime) > timerDelay) {
    //Check WiFi connection status
    if(WiFi.status()== WL_CONNECTED){
              
      sensorReadings = httpGETRequest(serverName);
      Serial.println(sensorReadings);
      JSONVar myObject = JSON.parse(sensorReadings);
  
      // JSON.typeof(jsonVar) can be used to get the type of the var
      if (JSON.typeof(myObject) == "undefined") {
        Serial.println("Parsing input failed!");
        return;
      }
    
      Serial.print("JSON object = ");
      Serial.println(myObject);
    
      // myObject.keys() can be used to get an array of all the keys in the object
      JSONVar keys = myObject.keys();
    
      for (int i = 0; i < keys.length(); i++) {
        JSONVar value = myObject[keys[i]];
        Serial.print(keys[i]);
        Serial.print(" = ");
        Serial.println(value);
        sensorReadingsArr[i] = double(value);
      }
      Serial.print("1 = ");
      Serial.println(sensorReadingsArr[0]);
      Serial.print("2 = ");
      Serial.println(sensorReadingsArr[1]);
      Serial.print("3 = ");
      Serial.println(sensorReadingsArr[2]);
    }
    else {
      Serial.println("WiFi Disconnected");
    }
    lastTime = millis();
  }
}

String httpGETRequest(const char* serverName) {
  WiFiClient client;
  HTTPClient http;
    
  // Your Domain name with URL path or IP address with path
  http.begin(client, serverName);
  
  // If you need Node-RED/server authentication, insert user and password below
  //http.setAuthorization("REPLACE_WITH_SERVER_USERNAME", "REPLACE_WITH_SERVER_PASSWORD");
  
  // Send HTTP POST request
  int httpResponseCode = http.GET();
  
  String payload = "{}"; 
  
  if (httpResponseCode>0) {
    Serial.print("HTTP Response code: ");
    Serial.println(httpResponseCode);
    payload = http.getString();
  }
  else {
    Serial.print("Error code: ");
    Serial.println(httpResponseCode);
  }
  // Free resources
  http.end();

  return payload;
}

```

[View raw code](https://github.com/RuiSantosdotme/Random-Nerd-Tutorials/raw/master/Projects/ESP32/HTTP/ESP32_HTTP_GET_JSON.ino)

### Setting your serverName

Enter your domain name or Node-RED IP address, so the ESP requests the sensor readings that will be retrieved in a JSON object.

```c
String serverName = "http://192.168.1.106:1880/get-sensor";
```

Now, upload the code to your board.

### HTTP GET Request (JSON Object)

In the  loop(), call the  httpGETRequest()  function to make the HTTP GET request:

```c
sensorReadings = httpGETRequest(serverName);
```

The  httpGETRequest()  function makes a request to Node-RED address  _http://192.168.1.106:1880/get-sensor_  and it retrieves a string with a JSON object.

```c
String httpGETRequest(const char* serverName) {
  HTTPClient http;

  // Your IP address with path or Domain name with URL path 
  http.begin(serverName);

  // If you need Node-RED/server authentication, insert user and password below
  //http.setAuthorization("REPLACE_WITH_SERVER_USERNAME", "REPLACE_WITH_SERVER_PASSWORD");


  // Send HTTP POST request
  int httpResponseCode = http.GET();

  String payload = "{}"; 

  if (httpResponseCode>0) {
    Serial.print("HTTP Response code: ");
    Serial.println(httpResponseCode);
    payload = http.getString();
  }
  else {
    Serial.print("Error code: ");
    Serial.println(httpResponseCode);
  }
  // Free resources
  http.end();

  return payload;
}
```

Note: if Node-RED requires authentication, uncomment the following line and insert the Node-RED username and password.

```c
// If you need Node-RED/server authentication, insert user and password below
//http.setAuthorization("REPLACE_WITH_SERVER_USERNAME", "REPLACE_WITH_SERVER_PASSWORD");
```

### Decoding JSON Object

To get access to the values, decode the JSON object and store all values in the  sensorReadingsArr  array.

```c
JSONVar myObject = JSON.parse(sensorReadings);

// JSON.typeof(jsonVar) can be used to get the type of the var
if (JSON.typeof(myObject) == "undefined") {
  Serial.println("Parsing input failed!");
  return;
}

Serial.print("JSON object = ");
Serial.println(myObject);

// myObject.keys() can be used to get an array of all the keys in the object
JSONVar keys = myObject.keys();

for (int i = 0; i < keys.length(); i++) {
  JSONVar value = myObject[keys[i]];
  Serial.print(keys[i]);
  Serial.print(" = ");
  Serial.println(value);
  sensorReadingsArr[i] = double(value);
}
Serial.print("1 = ");
Serial.println(sensorReadingsArr[0]);
Serial.print("2 = ");
Serial.println(sensorReadingsArr[1]);
Serial.print("3 = ");
Serial.println(sensorReadingsArr[2]);
```

### HTTP GET Demonstration

After uploading the code, open the Arduino IDE and you’ll see that it’s receiving the following JSON data:

```
{"value1":24.25,"value2":49.54,"value3":1005.14}
```

Then, you print the decoded JSON object in the Arduino IDE Serial Monitor.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2020/04/JSON-Data-object-HTTP-POST-Request-Method.png?resize=828%2C382&quality=100&strip=all&ssl=1)

For debugging purposes, the requested information is also printed in the Node-RED debug window.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2020/04/udpate-data-esp32-HTTP-GET-ESP8266-NodeMCU-Arduino-JSON-object.png?resize=434%2C221&quality=100&strip=all&ssl=1)

## 3. ESP32 HTTP POST: URL Encoded, JSON Data Object, Plain Text

Finally, you’ll learn how to make an HTTP POST request with an ESP32.

With this example, your ESP32 can make HTTP POST requests using three different types of body requests: URL encoded, JSON object or plain text. These are the most common methods and should integrate with most APIs or web services.

![HTTP POST ESP32 URL Encoded JSON Object Data Plain Text](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2020/04/HTTP-POST-ESP32-URL-Encoded-JSON-Object-Data-Plain-Text.png?resize=828%2C392&quality=100&strip=all&ssl=1)

Copy the next sketch to your Arduino IDE (type your SSID and password):

```c
#include <WiFi.h>
#include <HTTPClient.h>

const char* ssid = "REPLACE_WITH_YOUR_SSID";
const char* password = "REPLACE_WITH_YOUR_PASSWORD";

//Your Domain name with URL path or IP address with path
const char* serverName = "http://192.168.1.106:1880/update-sensor";

// the following variables are unsigned longs because the time, measured in
// milliseconds, will quickly become a bigger number than can be stored in an int.
unsigned long lastTime = 0;
// Timer set to 10 minutes (600000)
//unsigned long timerDelay = 600000;
// Set timer to 5 seconds (5000)
unsigned long timerDelay = 5000;

void setup() {
  Serial.begin(115200);

  WiFi.begin(ssid, password);
  Serial.println("Connecting");
  while(WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.print("Connected to WiFi network with IP Address: ");
  Serial.println(WiFi.localIP());
 
  Serial.println("Timer set to 5 seconds (timerDelay variable), it will take 5 seconds before publishing the first reading.");
}

void loop() {
  //Send an HTTP POST request every 10 minutes
  if ((millis() - lastTime) > timerDelay) {
    //Check WiFi connection status
    if(WiFi.status()== WL_CONNECTED){
      WiFiClient client;
      HTTPClient http;
    
      // Your Domain name with URL path or IP address with path
      http.begin(client, serverName);
      
      // If you need Node-RED/server authentication, insert user and password below
      //http.setAuthorization("REPLACE_WITH_SERVER_USERNAME", "REPLACE_WITH_SERVER_PASSWORD");
      
      // Specify content-type header
      http.addHeader("Content-Type", "application/x-www-form-urlencoded");
      // Data to send with HTTP POST
      String httpRequestData = "api_key=tPmAT5Ab3j7F9&sensor=BME280&value1=24.25&value2=49.54&value3=1005.14";           
      // Send HTTP POST request
      int httpResponseCode = http.POST(httpRequestData);
      
      // If you need an HTTP request with a content type: application/json, use the following:
      //http.addHeader("Content-Type", "application/json");
      //int httpResponseCode = http.POST("{\"api_key\":\"tPmAT5Ab3j7F9\",\"sensor\":\"BME280\",\"value1\":\"24.25\",\"value2\":\"49.54\",\"value3\":\"1005.14\"}");

      // If you need an HTTP request with a content type: text/plain
      //http.addHeader("Content-Type", "text/plain");
      //int httpResponseCode = http.POST("Hello, World!");
     
      Serial.print("HTTP Response code: ");
      Serial.println(httpResponseCode);
        
      // Free resources
      http.end();
    }
    else {
      Serial.println("WiFi Disconnected");
    }
    lastTime = millis();
  }
}

```

[View raw code](https://github.com/RuiSantosdotme/Random-Nerd-Tutorials/raw/master/Projects/ESP32/HTTP/ESP32_HTTP_POST.ino)

### Setting your serverName

Enter your domain name or Node-RED IP address, so the ESP posts sample sensor readings.

```c
String serverName = "http://192.168.1.106:1880/update-sensor";
```

Now, upload the code to your board.

### HTTP POST URL Encoded

To make an HTTP POST request of type URL encoded, like this

```
POST /update-sensor HTTP/1.1
Host: 192.168.1.106:1880
api_key=tPmAT5Ab3j7F9&sensor=BME280&value1=24.25&value2=49.54&value3=1005.14
Content-Type: application/x-www-form-urlencoded
```

You need to run the following in your Arduino code:

```c
// Your Domain name with URL path or IP address with path
http.begin(serverName);

// If you need Node-RED/server authentication, insert user and password below
//http.setAuthorization("REPLACE_WITH_SERVER_USERNAME", "REPLACE_WITH_SERVER_PASSWORD");

// Specify content-type header
http.addHeader("Content-Type", "application/x-www-form-urlencoded");

// Data to send with HTTP POST
String httpRequestData = "api_key=tPmAT5Ab3j7F9&sensor=BME280&value1=24.25&value2=49.54&value3=1005.14";

// Send HTTP POST request
int httpResponseCode = http.POST(httpRequestData);
```

Note: if Node-RED requires authentication, uncomment the following line and insert the Node-RED username and password.

```c
// If you need Node-RED/server authentication, insert user and password below
//http.setAuthorization("REPLACE_WITH_SERVER_USERNAME", "REPLACE_WITH_SERVER_PASSWORD");
```

### HTTP POST JSON Object

Or if you prefer to make an HTTP POST request with a JSON object:

```
POST /update-sensor HTTP/1.1
Host: example.com
{api_key: "tPmAT5Ab3j7F9", sensor_name: "BME280", temperature: 24.25; humidity: 49.54; pressure: 1005.14}
Content-Type: application/json
```

Use the next snippet:

```c
http.addHeader("Content-Type", "application/json");

int httpResponseCode = http.POST("{\"api_key\":\"tPmAT5Ab3j7F9\",\"sensor\":\"BME280\",\"value1\":\"24.25\",\"value2\":\"49.54\",\"value3\":\"1005.14\"}");
```

### HTTP Plain Text

If you want to send plain text or a value, use the following:

```c
http.addHeader("Content-Type", "text/plain");

int httpResponseCode = http.POST("Hello, World!");
```

**Note:** the Node-RED flow we’re using (web service) is not setup to receive plain text, but if the API that you plan to integrate only accepts plain text or a value, you can use the previous snippet.

### HTTP POST Demonstration

In the Node-RED debug window, you can view that your ESP is making an HTTP POST request every 5 seconds.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2020/04/udpate-data-esp32-HTTP-POST-ESP8266-NodeMCU-Arduino-value.png?resize=439%2C236&quality=100&strip=all&ssl=1)

And in this example, those values are also sent to 3 Gauges and are displayed in Node-RED Dashboard:

```
http://raspberry-pi-ip-address:1880/ui
```

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2020/04/ESP32-ESP8266-HTTP-GET-POST-Demonstration.png?resize=518%2C806&quality=100&strip=all&ssl=1)
