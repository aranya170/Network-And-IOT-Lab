
# Lab 08: Google Sheets via Apps Script: ESP32/ESP8266 Publish Sensor Readings to Google Sheets

In this tutorial we’re going to show you how to publish sensor readings to Google Sheets using ESP32 or ESP8266 board. As an example, we’ll publish temperature, humidity, and pressure readings using the BME280 sensor to a Google Sheets spreadsheet every 30 minutes – we’ll be using IFTTT.

## Creating Your IFTTT Account

For this project we’ll be using IFTTT to integrate with Google Sheets. So, the first step is creating an account on IFTTT if you don’t have one. Creating an account on IFTTT is free!

Go the official site: [ifttt.com](https://ifttt.com/) and enter your email to get started.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/ifttt.jpg?resize=750%2C353&quality=100&strip=all&ssl=1)

## Creating an Applet

Next, you need to create a new applet. Follow the next steps to create a new applet:

**1)**  Go to “My Applets”and create a new applet by clicking the “New Applet” button.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/1_new_applet-1.png?resize=828%2C200&quality=100&strip=all&ssl=1)

**2)**  Click on the “this” word that is in a blue color – as highlighted in the figure below.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/2_this.png?resize=760%2C203&quality=100&strip=all&ssl=1)

  
**3)**  Search for the “Webhooks” service and select the Webhooks icon.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/3_webhooks.png?resize=547%2C340&quality=100&strip=all&ssl=1)

**4)**  Choose the “Receive a web request” trigger.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/4_receive_request.png?resize=346%2C387&quality=100&strip=all&ssl=1)

**5)**  Give a name to the event. In this case “**bme280_readings**” as shown in the figure below. Then, click the “Create trigger” button.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/5_create_trigger.png?resize=489%2C525&quality=100&strip=all&ssl=1)

**6)**  Click the “that” word to proceed.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/6_that.png?resize=680%2C104&quality=100&strip=all&ssl=1)

**7)**  Search for the “Google Sheets” service, and select the Google Sheets icon.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/7_google_sheets.png?resize=548%2C354&quality=100&strip=all&ssl=1)

**8)**  If you haven’t connected with the Google Sheets service yet, you need to click the “Connect” button.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/connect-google-sheets.jpg?resize=500%2C268&quality=100&strip=all&ssl=1)

**9)**  Choose the “Add a row to spreadsheet” action.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/8_select_add_row.png?resize=478%2C342&quality=100&strip=all&ssl=1)

**10)**  Then, complete the action fields. Give the spreadsheet a name, leave the “Formatted row” field as default, and then, choose a Google Drive folder path. If you leave this field empty, IFTTT will create a folder called “IFTTT” in your Google Drive folder to save the spreadsheet. Finally, click the “Create action” button.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/9_create_action.png?resize=480%2C830&quality=100&strip=all&ssl=1)

**11)**  Your applet should be created after you press the “Finish” button.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/10_review_and_finish.png?resize=342%2C454&quality=100&strip=all&ssl=1)

## Testing Your Applet

Before proceeding with the project, it is very important to test your applet first. Follow the next steps to test your applet.

**1)**  Go to the  [Webhooks Service page](https://ifttt.com/maker_webhooks), and click the “Documentation” button.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/1_maker_webhooks.png?resize=602%2C261&quality=100&strip=all&ssl=1)

**2)**  A page as shown in the following figure will appear. The page shows your unique API key. You shouldn’t share your unique API key with anyone.

Fill the “To trigger an Event” section as shown below – it is highlighted with red rectangles. Then, click the “Test it” button.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/2_unique_webhooks_test_page.png?resize=828%2C536&quality=100&strip=all&ssl=1)

**3)**  The event should be successfully triggered, and you’ll get a green message as shown below saying “Event has been triggered”.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/3_success_event_triggered.png?resize=750%2C171&quality=100&strip=all&ssl=1)

**4)**  Go to your Google Drive. The IFTTT service should have created a folder called “IFTTT” with the “BME280_Readings” spreadsheet inside.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/5_open_spreadsheet.png?resize=531%2C478&quality=100&strip=all&ssl=1)

**5)**  Open the spreadsheet, and you should see the values you’ve filled previously to test the applet.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/6_success_test.png?resize=828%2C421&quality=100&strip=all&ssl=1)

Continue reading this post to see how to integrate the IFTTT Google Sheets service with your ESP32 or ESP8266.

## Schematics

The BME280 sensor we’re using in this example can communicate with the ESP32/ESP8266 using I2C communication protocol. So, we’re going to use the ESP I2C pins.

### BME280 with ESP32

Follow the next schematic diagram to wire the BME280 sensor if you’re using an ESP32.

[![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/ESP32-bme280_bb.png?resize=750%2C652&quality=100&strip=all&ssl=1)](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/03/ESP32-bme280_bb.png?quality=100&strip=all&ssl=1)

_(This schematic uses the ESP32 DEVKIT V1 module version with 36 GPIOs – if you’re using another model, please check the pinout for the board you’re using.)_

## Installing the BME280 library

To take readings from the BME280 sensor module we’ll use the [Adafruit_BME280 library](https://github.com/adafruit/Adafruit_BME280_Library). Follow the next steps to install the library in your Arduino IDE:

Open your Arduino IDE and go to **Sketch** > **Include Library** > **Manage Libraries**. The Library Manager should open.

Search for “**adafruit bme280** ” on the Search box and install the library.

![Installing the BME280 library Arduino IDE](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/08/install-bme280.png?resize=786%2C443&quality=100&strip=all&ssl=1)

## Installing the Adafruit_Sensor library

To use the BME280 library, you also need to install the [Adafruit_Sensor library](https://github.com/adafruit/Adafruit_Sensor). Follow the next steps to install the library in your Arduino IDE:

Go to **Sketch** > **Include Library** > **Manage Libraries** and type “**Adafruit Unified Sensor**” in the search box. Scroll all the way down to find the library and install it.

![Installing Adafruit Unified Sensor library Arduino IDE](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2019/04/adafruit_unified_sensor_library.png?resize=750%2C422&quality=100&strip=all&ssl=1)

After installing the libraries, restart your Arduino IDE.

## Code
```c
#ifdef ESP32
  #include <WiFi.h>
#else
  #include <ESP8266WiFi.h>
#endif

#include <Wire.h>
#include <Adafruit_Sensor.h>
#include <Adafruit_BME280.h>

// Replace with your SSID and Password
const char* ssid     = "REPLACE_WITH_YOUR_SSID";
const char* password = "REPLACE_WITH_YOUR_PASSWORD";

// Replace with your unique IFTTT URL resource
const char* resource = "REPLACE_WITH_YOUR_IFTTT_URL_RESOURCE";

// How your resource variable should look like, but with your own API KEY (that API KEY below is just an example):
//const char* resource = "/trigger/bme280_readings/with/key/nAZjOphL3d-ZO4N3k64-1A7gTlNSrxMJdmqy3";

// Maker Webhooks IFTTT
const char* server = "maker.ifttt.com";

// Time to sleep
uint64_t uS_TO_S_FACTOR = 1000000;  // Conversion factor for micro seconds to seconds
// sleep for 30 minutes = 1800 seconds
uint64_t TIME_TO_SLEEP = 1800;

// Uncomment to use BME280 SPI
/*#include <SPI.h>
#define BME_SCK 13
#define BME_MISO 12
#define BME_MOSI 11
#define BME_CS 10*/

#define SEALEVELPRESSURE_HPA (1013.25)

Adafruit_BME280 bme; // I2C
//Adafruit_BME280 bme(BME_CS); // hardware SPI
//Adafruit_BME280 bme(BME_CS, BME_MOSI, BME_MISO, BME_SCK); // software SPI

void setup() {
  Serial.begin(115200); 
  delay(2000);

  // initialize BME280 sensor
  bool status;
  status = bme.begin(0x76);  
  if (!status) {
    Serial.println("Could not find a valid BME280 sensor, check wiring!");
    while (1);
  }

  initWifi();
  makeIFTTTRequest();
    
  #ifdef ESP32
    // enable timer deep sleep
    esp_sleep_enable_timer_wakeup(TIME_TO_SLEEP * uS_TO_S_FACTOR);    
    Serial.println("Going to sleep now");
    // start deep sleep for 3600 seconds (60 minutes)
    esp_deep_sleep_start();
  #else
    // Deep sleep mode for 3600 seconds (60 minutes)
    Serial.println("Going to sleep now");
    ESP.deepSleep(TIME_TO_SLEEP * uS_TO_S_FACTOR); 
  #endif
}

void loop() {
  // sleeping so wont get here 
}

// Establish a Wi-Fi connection with your router
void initWifi() {
  Serial.print("Connecting to: "); 
  Serial.print(ssid);
  WiFi.begin(ssid, password);  

  int timeout = 10 * 4; // 10 seconds
  while(WiFi.status() != WL_CONNECTED  && (timeout-- > 0)) {
    delay(250);
    Serial.print(".");
  }
  Serial.println("");

  if(WiFi.status() != WL_CONNECTED) {
     Serial.println("Failed to connect, going back to sleep");
  }

  Serial.print("WiFi connected in: "); 
  Serial.print(millis());
  Serial.print(", IP address: "); 
  Serial.println(WiFi.localIP());
}

// Make an HTTP request to the IFTTT web service
void makeIFTTTRequest() {
  Serial.print("Connecting to "); 
  Serial.print(server);
  
  WiFiClient client;
  int retries = 5;
  while(!!!client.connect(server, 80) && (retries-- > 0)) {
    Serial.print(".");
  }
  Serial.println();
  if(!!!client.connected()) {
    Serial.println("Failed to connect...");
  }
  
  Serial.print("Request resource: "); 
  Serial.println(resource);

  // Temperature in Celsius
  String jsonObject = String("{\"value1\":\"") + bme.readTemperature() + "\",\"value2\":\"" + (bme.readPressure()/100.0F)
                      + "\",\"value3\":\"" + bme.readHumidity() + "\"}";
                      
  // Comment the previous line and uncomment the next line to publish temperature readings in Fahrenheit                    
  /*String jsonObject = String("{\"value1\":\"") + (1.8 * bme.readTemperature() + 32) + "\",\"value2\":\"" 
                      + (bme.readPressure()/100.0F) + "\",\"value3\":\"" + bme.readHumidity() + "\"}";*/
                      
  client.println(String("POST ") + resource + " HTTP/1.1");
  client.println(String("Host: ") + server); 
  client.println("Connection: close\r\nContent-Type: application/json");
  client.print("Content-Length: ");
  client.println(jsonObject.length());
  client.println();
  client.println(jsonObject);
        
  int timeout = 5 * 10; // 5 seconds             
  while(!!!client.available() && (timeout-- > 0)){
    delay(100);
  }
  if(!!!client.available()) {
    Serial.println("No response...");
  }
  while(client.available()){
    Serial.write(client.read());
  }
  
  Serial.println("\nclosing connection");
  client.stop(); 
}

```

[View raw code](https://github.com/RuiSantosdotme/Random-Nerd-Tutorials/raw/master/Projects/ESP32_ESP8266_BME280_Google_Sheets.ino)

### Including your SSID and password

The first thing you need to modify in the code is writing your network credentials: the SSID and password on the following lines:

```c
// Replace with your SSID and Password
const char* ssid = "REPLACE_WITH_YOUR_SSID";
const char* password = "REPLACE_WITH_YOUR_PASSWORD";
```

### Including your unique IFTTT URL resource

Then, you need to write your unique IFTTT URL resource. Go back to “Testing your Applet” section bullet 2) to get your unique IFTTT URL resource.

```c
// Replace with your unique IFTTT URL resource
const char* resource = "REPLACE_WITH_YOUR_IFTTT_URL_RESOURCE";
```

In my case, my resource is:

```c
/trigger/bme280_readings/with/key/nAZjOphL3d-ZO4N3k64-1A7gTlNSrxMJdmqy3
```

So, that line in the code looks as follows:

```c
const char* resource = "/trigger/bme280_readings/with/key/nAZjOphL3d-ZO4N3k64-1A7gTlNSrxMJdmqy3";
```

### Setting the sleep time

In this example we’ve set the sleep time to 30 minutes. This means that every 30 minutes the ESP wakes up, takes the readings, and publishes in your Google Sheets spreadsheet. The sleep time is set in the  TIME_TO_SLEEP  variable in seconds:

```c
// sleep for 30 minutes = 1800 seconds
uint64_t TIME_TO_SLEEP = 1800;
```

If you want to change the sleep time, you need to change the  TIME_TO_SLEEP  variable. Note that you should enter the sleep time in the  TIME_TO_SLEEP  variable in seconds.

### Sending the BME280 readings

The BME280 sensor readings are sent using the  jsonObject  variable as shown in the following line:

```
String jsonObject = String("{\"value1\":\"") + bme.readTemperature() + "\",\"value2\":\"" + (bme.readPressure()/100.0F) + "\",\"value3\":\"" + bme.readHumidity() + "\"}";
```

### Publish temperature in Fahrenheit

In order to publish the temperature in Fahrenheit, you need to comment and uncomment the code like this:

```c
// Temperature in Celsius
/*String jsonObject = String("{\"value1\":\"") + bme.readTemperature() + "\",\"value2\":\"" + (bme.readPressure()/100.0F) + "\",\"value3\":\"" + bme.readHumidity() + "\"}";*/

// Comment the previous line and uncomment the next line to publish temperature readings in Fahrenheit 
String jsonObject = String("{\"value1\":\"") + (1.8 * bme.readTemperature() + 32) + "\",\"value2\":\"" + (bme.readPressure()/100.0F) + "\",\"value3\":\"" + bme.readHumidity() + "\"}";
```

## Demonstration

After making all the necessary changes. Upload the code to your ESP32 or ESP8266. Make sure you select the right board and COM port.

Every 30 minutes, the ESP32 or ESP8266 wakes up to take sensor readings and publishes the readings in a spreadsheet on Google Sheets.

![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/04/google_sheets_readings.png?resize=828%2C518&quality=100&strip=all&ssl=1)

The ESP32 chip has a built-in clock, so the readings are very accurate and it publishes to the spreadsheet every 30 minutes. On the other hand, the ESP8266 publishes new readings approximately every 28 to 29 minutes.
